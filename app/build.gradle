apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'jacoco'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.jim.sharetocomputer"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 1120
        versionName "1.1.2"
        testInstrumentationRunner "com.jim.sharetocomputer.TestRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }
    dataBinding {
        enabled = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }
    androidExtensions {
        experimental = true
    }
    testOptions {
        animationsDisabled true

        unitTests {
            includeAndroidResources = true
        }
    }
    lintOptions {
        textReport = "stdout"
        warningsAsErrors true
        abortOnError true
    }
    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }

    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'
}

configurations.all {
    resolutionStrategy.force "androidx.test:core:$testExtCore"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "androidx.appcompat:appcompat:$ktxVersion"
    implementation "androidx.core:core-ktx:$ktxVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
    implementation "org.nanohttpd:nanohttpd:$nanohttpd"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"
    implementation "org.koin:koin-android:$koinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinCoroutinesVersion"
    implementation "org.koin:koin-android-viewmodel:$koinVersion"
    implementation "com.google.code.gson:gson:$gsonVersion"
    implementation "com.journeyapps:zxing-android-embedded:$zxingVersion"
    implementation "com.elvishew:xlog:$xlogVersion"
    implementation "androidx.preference:preference:$androidxPreferenceVersion"
    debugImplementation 'androidx.fragment:fragment-testing:1.2.0-alpha01'

    testImplementation "junit:junit:$junitVersion"
    //noinspection GradleDependency
    testImplementation "androidx.test:core:$testExtCore"
    testImplementation "androidx.test.ext:junit:$testExtJunit"
    testImplementation "org.robolectric:robolectric:$robolectric"
    testImplementation "org.koin:koin-test:$koinVersion"
    testImplementation "androidx.test.espresso:espresso-intents:$espressoVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlinCoroutinesVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espressoVersion"
    androidTestImplementation 'tools.fastlane:screengrab:1.1.0'
    androidTestImplementation "androidx.test:runner:$testExtCore"

    androidTestUtil 'com.android.support.test:orchestrator:1.0.2'
}

jacoco {
    toolVersion = '0.8.0'
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = [ '**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*' ]
    def debugTree = fileTree(dir: "$project.buildDir/tmp/kotlin-classes/debug", excludes: fileFilter)
    def mainSrc = "$project.projectDir/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = fileTree(dir: project.buildDir, includes: [
            'jacoco/*.exec', 'outputs/code_coverage/debugAndroidTest/connected/*coverage.ec'
    ])
}
